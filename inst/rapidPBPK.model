States = {inhswch,ainh,aexh,totodose,odose,totddose,ddose,odosev, totodosev, alas, akent, afec, aabsgut, ivswch,aiv,abld,abfat,atfat,abskin,atskin,abmusc,atmusc,abbone,atbone,abbrn,atbrn,ablng,atlng,abhrt,athrt,abgi,atgi,abliv,atliv,abkdn,atkdn,abrpf,atrpf,abspf,atspf,ametliv1,ametliv2,aclbld,auexc,anabsgut,aucpls};
Outputs = {abone,abrn,alng,ahrt,akdn,agi,aliv,arpf,aspf,afat,askin,amusc,cpls,cv,cfat_um,ctfat,cbfat,cskin_um,ctskin,cbskin,cmusc_um,ctmusc,cbmusc,cbone_um,ctbone,cbbone,cbrn_um,ctbrn,cbbrn,clng_um,ctlng,cblng,chrt_um,cthrt,cbhrt,ckdn_um,ctkdn,cbkdn,cgi_um,ctgi,cbgi,cliv_um,ctliv,cbliv,crpf_um,ctrpf,cbrpf,cspf_um,ctspf,cbspf,InstInhDose,mbal};

#PARAMETERS 
#Constant values
mw = 0;

bdose=0;
blen=0;
breps=0;
totbreps = 0;
drdose=0;
vdw=0;
dreps=0;
bdosev=0;
blenv = 0;
brepsv = 0;
totbrepsv = 0;
kfec=0;		# fecal excretion (1/hr, some function of transit time)
kVtoL=0;	# transfer from dosing vehicle to luminal absorption site (1/hr)
kent=0;		# first order metabolism in enterocytes (1/hr)

inhdose=0;
inhtlen=0;
inhdays=0;
ivdose=0;
ivlen=0;
bw=0;
qcc=0;
hct=0;
vbldc=0;
perfc=0;
kbld=0;
respr=0;
tv=0;
ds=0;
uflw=0;
gfr=0;
frwsol = 0;
fatvtbc=0;
vfatc=0;
qfatc=0;
pfat=0;

skinvtbc=0;
vskinc=0;
qskinc=0;
pskin=0;

muscvtbc=0;
vmuscc=0;
qmuscc=0;
pmusc=0;

bonevtbc=0;
vbonec=0;
qbonec=0;
pbone=0;

brnvtbc=0;
vbrnc=0;
qbrnc=0;
pbrn=0;

lngvtbc=0;
vlngc=0;
qlngc=0;
plng=0;

hrtvtbc=0;
vhrtc=0;
qhrtc=0;
phrt=0;

givtbc=0;
vgic=0;
qgic=0;
pgi=0;
fa=0;
ka=0;

livvtbc=0;
vlivc=0;
qalivc=0;
qvlivc=0;
pliv=0;

kdnvtbc = 0;
vkdnc=0;
qkdnc=0;
pkdn=0;

rpfvtbc=0;
vrpfc=0;
qrpfc=0;
prpf=0;

spfvtbc=0;
vspfc=0;
qspfc=0;
pspf=0;

res=0;

fupls=0;




# Calculated values
vbld = 0;
vpls = 0;
vfat = 0;
vskin = 0;
vmusc = 0;
vbone = 0;
vbrn = 0;
vlng = 0;
vhrt = 0;
vkdn = 0;
vgi = 0;
vliv = 0;
vrpf = 0;
vspf = 0;

#Total Fractional Perfusion
total_perf = 0;  # This does not include flow to GI since that is a part of liver venous flow

#Scaled Perfusion
qcp = 0;
qfat = 0;
qskin = 0;
qmusc =0;
qbone =0;
qbrn = 0;
qlng = 0;
qhrt = 0;
qkdn = 0;
qvliv = 0;
qgi = 0;
qaliv = 0;
qrpf = 0;
qspf = 0;

#Scaled tissue permeability coefs
pafat = 0;
paskin = 0;
pamusc = 0;
pabone = 0;
pabrn = 0;
palng = 0;
pahrt = 0;
pakdn = 0;
pagi =0;
paliv = 0;
parpf = 0;
paspf = 0;

vkm1 =0;
vmaxliv =0;
km = 0;



cinh = 0;
qalv = 0;
pair = 1e10;



		  
Dynamics{
      ###################### CONCENTRATION CALCULATIONS ###########################

      #Blood Plasma (arterial)
      cpls = abld/vpls;


      # fat
      ctfat = atfat/(fatvtbc*vfat) ;          # fat Tissue Concentration
      cbfat = abfat/((1-fatvtbc)*vfat) ;      # fat Blood Concentration
      afat = atfat + abfat;
      cfat_um = (atfat+abfat)/vfat;


      # Skin
      ctskin = atskin/(skinvtbc*vskin)   ;    # Skin Tissue Concentration
      cbskin = abskin/((1-skinvtbc)*vskin)  ; # SKin Blood Concentration
      askin = atskin+abskin;
      cskin_um = (atskin+abskin)/vskin;


      # Muscle
      ctmusc = atmusc/(muscvtbc*vmusc)  ;     # Muscle Tissue Concentration
      cbmusc = abmusc/((1-muscvtbc)*vmusc) ;  # Muscle Blood Concentration
      amusc = atmusc+abmusc;
      cmusc_um = (atmusc+abmusc)/vmusc;


      # Bone
      ctbone = atbone/(bonevtbc*vbone)  ;     # Bone Tissue Concentration
      cbbone = abbone/((1-bonevtbc)*vbone) ;  # Bone Blood Concentration
      abone = atbone+abbone;
      cbone_um = (atbone+abbone)/vbone;


      # Brain
      ctbrn = atbrn/(brnvtbc*vbrn) ;      # Brain Tissue Concentration
      cbbrn = abbrn/((1-brnvtbc)*vbrn);   # Brain Blood Concentration
      abrn = atbrn+abbrn;
      cbrn_um = (atbrn+abbrn)/vbrn;
 

      # Lung
      ctlng = atlng/(lngvtbc*vlng)    ;   # Lung Tissue Concentration
      cblng = ablng/((1-lngvtbc)*vlng) ;  # Lung Blood Concentration
      alng = atlng+ablng;
      clng_um = (atlng+ablng)/vlng;


      # Heart
      cthrt = athrt/(hrtvtbc*vhrt)     ;  # Heart Tissue Concentration
      cbhrt = abhrt/((1-hrtvtbc)*vhrt)   ;# Heart Blood Concentration
      ahrt = athrt+abhrt;
      chrt_um = (athrt+abhrt)/vhrt;


      # Kidney
      ctkdn = atkdn/(kdnvtbc*vkdn)    ;   # Kidney Tissue Concentration
      cbkdn = abkdn/((1-kdnvtbc)*vkdn) ;  # Kidney Blood Concentration
      akdn = atkdn+abkdn;
      ckdn_um = (atkdn+abkdn)/vkdn;


      # GI
      ctgi = atgi/(givtbc*vgi)  ;     # GI Tissue Concentration
      cbgi = abgi/((1-givtbc)*vgi)  ; # GI Blood Concentration
      agi = atgi+abgi;
      cgi_um = (atgi+abgi)/vgi;

      # Liver
      ctliv = atliv/(livvtbc*vliv) ;      # Liver Tissue Concentration
      cbliv = abliv/((1-livvtbc)*vliv);  # Liver Blood Concentration
      aliv = atliv+abliv;
      cliv_um = (atliv+abliv)/vliv;


      # Rapidly Perfused Tissue
      ctrpf = atrpf/(rpfvtbc*vrpf) ;      # Rapidly Perfused Tissue Tissue Concentration
      cbrpf = abrpf/((1-rpfvtbc)*vrpf) ;  # Rapidly Perfused Tissue Blood Concentration
      arpf = atrpf+abrpf;
      crpf_um = (atrpf+abrpf)/vrpf;


      # Slowly Perfused Tissue
      ctspf = atspf/(spfvtbc*vspf)   ;    # Slowly Perfused Tissue Tissue Concentration
      cbspf = abspf/((1-spfvtbc)*vspf)  ; # Slowly Perfused Tissue Blood Concentration
      aspf = atspf+abspf;
      cspf_um = (atspf+abspf)/vspf;


      #Blood plasma(venous)
      cv = (qfat*cbfat+qskin*cbskin+qmusc*cbmusc+qbone*cbbone+qbrn*cbbrn+qlng*cblng+qhrt*cbhrt+qkdn*cbkdn+qvliv*cbliv+qrpf*cbrpf+qspf*cbspf)/qcp;

      ###################### EXPOSURE CALCULATIONS ##############################

      # Oral and Drinking Water Exposure


      #rate of oral and drinking water reductions
      dt(odose) = -ka*odose;
      dt(ddose) = -ka*ddose;
      # track tot dose as a state variable to it can plotted effectivly at the end
      dt(totodose) = 0;
      dt(totddose) = 0;
       
      # dose available in gut lumen at each time point
      dose_in_gut = ka*(odose+ddose);

      #change in amount of dose absorbed
      available_dose = dose_in_gut  ;  # chemical available for absorbtion 
      
      #Oral Vehicle exposure
      dt(totodosev)=0;
      dt(odosev)=-kfec*odosev-kVtoL*odosev;
      dt(alas) = kVtoL*odosev-kfec*alas-kent*alas;
      dt(akent)= kent*alas;
      dt(afec)= kfec*alas;
      
      
      raabsgut = ka*alas + fa*available_dose   ;                  # amount absorbed per time step
      dt(aabsgut) = raabsgut;
      ranabsgut = (1-fa)*available_dose  ;                 # amount not absorbed per time step
      dt(anabsgut) = ranabsgut;
      
      
      
      # Inhalation Exposure
      dt(inhswch) = 0;
      rinh = inhswch*qalv*cinh      ;                     #amount inhaled per time in lung tissue compartment
      dt(ainh) = rinh;
      rexh = inhswch*qalv*cpls/pair    ;                 #amount exhaled per time from lung tissue compartment
      dt(aexh) = rexh;

      # Inravenous Exposure
      dt(ivswch) = 0;
      riv = ivswch*ivdose		;						 #IV dose per time
      dt(aiv) =riv     ;                      

      ###################### RATE OF CHANGE CALCULATIONS #########################

      # fat
      dt(abfat) = qfat*cpls-qfat*cbfat + pafat*ctfat/pfat - pafat*cbfat;
      dt(atfat) = pafat*cbfat - pafat*ctfat/pfat;

      #skin
      dt(abskin) = qskin*cpls- qskin*cbskin + paskin*ctskin/pskin - paskin*cbskin;
      dt(atskin) = paskin*cbskin - paskin*ctskin/pskin;

      #muscle
      dt(abmusc) = qmusc*cpls- qmusc*cbmusc + pamusc*ctmusc/pmusc - pamusc*cbmusc;
      dt(atmusc) = pamusc*cbmusc - pamusc*ctmusc/pmusc;

      #bone
      dt(abbone) = qbone*cpls- qbone*cbbone + pabone*ctbone/pbone - pabone*cbbone;
      dt(atbone) = pabone*cbbone - pabone*ctbone/pbone;

      #brain
      dt(abbrn) = qbrn*cpls- qbrn*cbbrn + pabrn*ctbrn/pbrn - pabrn*cbbrn;
      dt(atbrn) = pabrn*cbbrn - pabrn*ctbrn/pbrn;

      #lung
      dt(ablng) = qlng*cpls- qlng*cblng + palng*ctlng/plng - palng*cblng + rinh - rexh;
      dt(atlng) = palng*cblng - palng*ctlng/plng;

      #heart
      dt(abhrt) = qhrt*cpls- qhrt*cbhrt + pahrt*cthrt/phrt - pahrt*cbhrt;
      dt(athrt) = pahrt*cbhrt - pahrt*cthrt/phrt;

      #kidney
      #dt(volurine) = uflw*bw ;                                             # dtrate of urine generation
      rauexc = gfr*frwsol*(1-res)*ctkdn/pkdn  ;                        # rate of change for amount of chem urinary excretion
      dt(auexc) = rauexc;
      dt(abkdn) = qkdn*cpls- qkdn*cbkdn + pakdn*ctkdn/pkdn - pakdn*cbkdn;
      dt(atkdn) = pakdn*cbkdn - pakdn*ctkdn/pkdn - rauexc;

      #GI
      dt(abgi) = qgi*cpls - qgi*cbgi + pagi*ctgi/pgi - pagi*cbgi+ raabsgut;
      dt(atgi) = pagi*cbgi - pagi*ctgi/pgi;
      #Liver
      rametliv1 = vkm1 * (ctliv*fupls/pliv);                              # amount metabolism in the liver linear
      dt(ametliv1) = rametliv1;
      rametliv2 = vmaxliv*(ctliv*fupls /pliv)/((ctliv /pliv) + km);      # amount metabolised in the liver saturable
      dt(ametliv2) = rametliv2;
      dt(abliv) = qaliv*cpls - qvliv*cbliv + qgi*cbgi + paliv*ctliv/pliv - paliv*cbliv;
      dt(atliv) = paliv*cbliv- paliv*ctliv/pliv - rametliv1 - rametliv2;

      #Rapidly Perfused Tissue
      dt(abrpf) = qrpf*cpls- qrpf*cbrpf + parpf*ctrpf/prpf - parpf*cbrpf;
      dt(atrpf) = parpf*cbrpf - parpf*ctrpf/prpf;

      #Slowly Perfused Tissue
      dt(abspf) = qspf*cpls- qspf*cbspf + paspf*ctspf/pspf - paspf*cbspf;
      dt(atspf) = paspf*cbspf - paspf*ctspf/pspf;


      #Blood
      raclbld = kbld*cpls*fupls;                                      # generic first order clearence in blood
      dt(aclbld) = raclbld;
      dt(abld) = qcp*(cv-cpls) + riv - raclbld;
      dt(aucpls)=cpls;



      ################################ MASS BALANCE ##############################

      dose = totodose + totddose + ainh + aiv +totodosev;
      body = abld + afat + askin + amusc + abone + abrn + alng + ahrt + agi + aliv + akdn + arpf + aspf  +odose +ddose+odosev+ alas;
      cleared = ametliv1+ametliv2 + aclbld + auexc + anabsgut + aexh + akent + afec;
      tmass = dose - body - cleared;
      }

CalcOutputs{
	InstInhDose=rinh;
	mbal=tmass;
}

Events {

odose = (bdose>0 ? odose + (bdose*bw*1000./mw)/totbreps : odose);
totodose = (bdose>0 ? totodose + (bdose*bw*1000./mw)/totbreps : totodose);

ddose = (drdose>0 ? ddose + (drdose*1000.*vdw/mw)/dreps : ddose);
totddose = (drdose>0 ? totddose + (drdose*1000.*vdw/mw)/dreps : totddose);

odosev = (bdosev>0? odosev + (bdosev*bw*1000./mw)/totbrepsv : odosev);

inhswch = (inhdose>0 ? (inhswch == 0 ? 1 : 0) : 0);
ivswch = (ivdose>0 ? (ivswch == 0 ? 1 : 0) : 0);
}

End
